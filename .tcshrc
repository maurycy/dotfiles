# $OpenBSD: dot.cshrc,v 1.7 2009/01/30 08:42:26 sobrado Exp $
#
# csh initialization

set path = (/usr/local/{bin,sbin} /usr/{bin,sbin} /{bin,sbin})

if ($?prompt) then
	# An interactive shell -- set some stuff up
	set autocorrect
	set autoexpand
	set echo_style = both
	set edit = vim
	set fignore = ( .o ~ % )
	set filec
	set noclobber
	set nobeep
	set noding
	set notify
	# set prompt = "[%P] [%l] [%h] %m:%B%n%b:%~%# "
	# set prompt = "%m:%B%n%b:%~%# "
	set prompt = "[%P] %m:%B%n%b:%~%# "
	set promptchars = '>#'
	set rmstar
	set watch = (0 any any)

	setenv EDITOR vim

	unset autorehash

	alias . source
	alias a 'tmux attach'
	alias d 'df -Hi'

	bindkey '^L' clear-screen
	bindkey '^W' backward-delete-word
	bindkey '^U' kill-whole-line
	bindkey "^X" list-choices

	if ($OSTYPE == 'darwin') then
		set path = ($home/local/{bin,sbin} /opt/local/{bin,sbin} $path)

		alias e 'open -a TextMate.app'
		alias l ls -lh
		alias ll ls -A@cFl
		alias precmd 'echo -n "\033]0;${HOST}:$cwd\007"'

		if ($HOST == 'g') then
			setenv ARCHFLAGS '-arch x86_64'
			setenv GOPATH $HOME/src/go
			setenv GOROOT `go env GOROOT`
			setenv HOMEBREW_GITHUB_API_TOKEN \
				0000000000000000000000000000000000000000
			setenv PKG_CONFIG_PATH "/opt/X11/lib/pkgconfig"

			set path = ($GOPATH/bin $path)

			unsetenv LC_CTYPE
		endif
	endif

	if ($OSTYPE == 'linux') then
		alias l ls -lh
		alias ll ls -AcfLl
		alias precmd 'echo -n "\033]0;${HOST}:$cwd\007"'
	endif

	cd .
	umask 22
endif
