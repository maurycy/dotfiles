# shell options
setopt EXTENDED_HISTORY
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY
setopt NO_BEEP
setopt EXTENDED_GLOB

# deduplicate PATH
typeset -U path

# remove history entries older than 7 days
() {
  local f=~/.zsh_history c=$((EPOCHSECONDS - 604800))
  [[ -f $f ]] && awk -v c=$c '/^: [0-9]+:/{if(int(substr($0,3))>=c)print;next}1' $f > $f.tmp && mv $f.tmp $f
}
HISTFILE=~/.zsh_history
HISTSIZE=10000000000000000
SAVEHIST=10000000000000000
zshaddhistory() {
  if [[ $1 == *postgres* ]]; then
    print -s "$(printf '%s' "${1%$'\n'}" | sed 's/[^ ]*postgres[^ ]*/meow/g')"
    return 1
  fi
}

# env
export EDITOR=vim
export NVM_DIR="$HOME/.nvm"
export BUN_INSTALL="$HOME/.bun"
export DISABLE_SPRING=1
{{- if eq .chezmoi.os "darwin" }}
export OBJC_DISABLE_INITIALIZE_FORK_SAFETY=YES
{{- end }}

{{- if eq .chezmoi.os "darwin" }}
# brew shellenv (cached for speed)
if [ -x /opt/homebrew/bin/brew ]; then
  _brew_cache="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/brew_shellenv.zsh"
  if [[ ! -f "$_brew_cache" ]]; then
    mkdir -p "${_brew_cache:h}"
    /opt/homebrew/bin/brew shellenv > "$_brew_cache"
  fi
  source "$_brew_cache"
  unset _brew_cache
fi
{{- end }}

# aliases
{{- if eq .chezmoi.os "darwin" }}
alias l='ls -lhG'
{{- else }}
alias l='ls -lh --color=auto'
{{- end }}
alias ll='l -a'
alias rg='rg -p'
alias be='bundle exec'
alias paw="pwgen -snyc 128"
alias python='uv run python'
alias yt='uvx yt-dlp -f bestvideo+bestaudio --write-all-thumbnails --write-sub --convert-subs srt'
{{- if eq .chezmoi.os "darwin" }}
alias code="'/Applications/Visual Studio Code.app/Contents/Resources/app/bin/code'"
alias code2="'/Applications/Visual Studio Code - Insiders.app/Contents/Resources/app/bin/code'"
alias e='code'
alias less='less -RX'
{{- end }}

# /usr/local/bin/rsync -s -E -v -a -x --delete -l -u -H --exclude-from=$EXCLUDE -P -e ssh $SRC $DEST
if command -v rsync >/dev/null; then
  alias f-rsync='rsync -s -E -v -a -x --delete -l -u -H -P'
  alias my-rsync='rsync -avhzrXANteEgoD --fileflags'
  alias cc-rsync='rsync -v -A -X -H -go --numeric-ids -D -l -rtpx -N --fileflags --force-change --protect-args --ignore-errors'
fi

if command -v git >/dev/null; then
  alias add='git add'
  alias ci='git ci'
  alias co='git co'
  alias push='git push'
  alias pull='git pull'
  alias st='git st'
  alias d='git diff'
  alias up='git up'
  alias stash='git stash'
  alias unstash='git stash apply'
  alias merge='git merge'
fi

# completion (cached for speed)
autoload -Uz compinit
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*:-command-:*' tag-order '!parameters'
autoload -U select-word-style
select-word-style bash

# prompt
autoload -Uz vcs_info
zstyle ':vcs_info:*' formats '(%b)'
precmd() {
  vcs_info
  PS1="%(?..[%?] )%D{%Y-%m-%dT%H:%M:%S.%N%z} %n@%B%m%b %d ${vcs_info_msg_0_} %# "
}

# path
[[ -d "$BUN_INSTALL/bin" ]] && path=("$BUN_INSTALL/bin" $path)

# tool initializations
command -v zoxide >/dev/null && eval "$(zoxide init zsh)"
[ -f "$HOME/.cargo/env" ] && source "$HOME/.cargo/env"

# bun completions
[ -s "$HOME/.bun/_bun" ] && source "$HOME/.bun/_bun"

# uv completion (cached for speed)
if command -v uv >/dev/null; then
  _uv_cache="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/uv.zsh"
  _uv_ver="# $(uv --version)"
  if [[ ! -f "$_uv_cache" || "$_uv_ver" != "$(head -1 "$_uv_cache" 2>/dev/null)" ]]; then
    mkdir -p "${_uv_cache:h}"
    echo "$_uv_ver" > "$_uv_cache"
    uv generate-shell-completion zsh >> "$_uv_cache"
  fi
  source "$_uv_cache"
  unset _uv_cache _uv_ver
fi

# google cloud sdk
[ -f "$HOME/.local/google-cloud-sdk/path.zsh.inc" ] && . "$HOME/.local/google-cloud-sdk/path.zsh.inc"
[ -f "$HOME/.local/google-cloud-sdk/completion.zsh.inc" ] && . "$HOME/.local/google-cloud-sdk/completion.zsh.inc"

# functions
diff() {
  local args=() use_git=0
  for arg in "$@"; do
    if [[ "$arg" == --cach* ]]; then
      args+=(--cached)
      use_git=1
    else
      args+=("$arg")
    fi
  done
  if (( use_git )); then
    git diff "${args[@]}"
  else
    command diff "$@"
  fi
}

_diff() {
  _arguments \
    '--cached[show git staged changes]' \
    '*:file:_files'
}
compdef _diff diff

_tab_or_diff_cycle() {
  if [[ "$BUFFER" == "diff" ]]; then
    BUFFER="git diff"
    CURSOR=$#BUFFER
  elif [[ "$BUFFER" == "git diff" ]]; then
    BUFFER="diff"
    CURSOR=$#BUFFER
  else
    zle expand-or-complete
  fi
}
zle -N _tab_or_diff_cycle
bindkey '^I' _tab_or_diff_cycle

worktree() {
  case "$1" in
    ls) shift; git worktree list "$@" ;;
    rm) shift; git worktree remove "$@" ;;
    *)  git worktree "$@" ;;
  esac
}

_is_brew_pkg() {
  command -v brew >/dev/null && brew list "$1" &>/dev/null
}

mupdate() {
  command -v chezmoi >/dev/null && { chezmoi update || return; }
  command -v uv >/dev/null && { uv self update || return; }

  if whence -w backup >/dev/null 2>&1; then
    backup || return
  fi

  command -v gcloud >/dev/null && { gcloud components update || return; }

  if command -v brew >/dev/null; then
    brew update && brew upgrade && brew cleanup --scrub --prune=all || return
  fi

  if command -v apt >/dev/null; then
    sudo apt update && sudo apt -yu dist-upgrade || return
    sudo apt autoclean && sudo apt autoremove -y || return
    deborphan 2>/dev/null | xargs -r sudo apt-get remove -y --purge
    sudo aptitude purge '~c' 2>/dev/null
  fi

  command -v rustup >/dev/null && { rustup update || return; }

  command -v rbenv >/dev/null && ! _is_brew_pkg rbenv && { rbenv update || true; }
  command -v bun >/dev/null && ! _is_brew_pkg bun && { bun upgrade || true; }
}

# rbenv (lazy-loaded)
if command -v rbenv >/dev/null 2>&1 || [ -d "$HOME/.rbenv" ]; then
  export RBENV_ROOT="${RBENV_ROOT:-$HOME/.rbenv}"
  [[ -d "$RBENV_ROOT/bin" ]] && path=("$RBENV_ROOT/bin" "$RBENV_ROOT/shims" $path)
  _rbenv_load() {
    unset -f ruby gem bundle rbenv irb rake rails
    eval "$(rbenv init - zsh)"
  }
  ruby() { _rbenv_load; ruby "$@"; }
  gem() { _rbenv_load; gem "$@"; }
  bundle() { _rbenv_load; bundle "$@"; }
  rbenv() { _rbenv_load; rbenv "$@"; }
  irb() { _rbenv_load; irb "$@"; }
  rake() { _rbenv_load; rake "$@"; }
  rails() { _rbenv_load; rails "$@"; }
fi

# nvm (lazy-loaded)
if [ -d "$NVM_DIR/versions/node" ]; then
  _nvm_resolve_alias() {
    local alias="$1" target
    while [ -f "$NVM_DIR/alias/$alias" ]; do
      target=$(cat "$NVM_DIR/alias/$alias")
      [[ "$target" == v* ]] && { echo "$target"; return; }
      alias="$target"
    done
    [ -f "$NVM_DIR/alias/lts/$alias" ] && cat "$NVM_DIR/alias/lts/$alias" && return
    echo "$alias"
  }
  NODE_VER=$(_nvm_resolve_alias default)
  if [ -d "$NVM_DIR/versions/node/$NODE_VER" ]; then
    path=("$NVM_DIR/versions/node/$NODE_VER/bin" $path)
  else
    NODE_LATEST=$(ls -v "$NVM_DIR/versions/node" 2>/dev/null | tail -1)
    [[ -n "$NODE_LATEST" ]] && path=("$NVM_DIR/versions/node/$NODE_LATEST/bin" $path)
  fi
  unset -f _nvm_resolve_alias
fi
if [ -s "$NVM_DIR/nvm.sh" ] || [ -s "/opt/homebrew/opt/nvm/nvm.sh" ]; then
  _nvm_load() {
    unset -f nvm node npm npx _nvm_load
    if [ -s "$NVM_DIR/nvm.sh" ]; then
      \. "$NVM_DIR/nvm.sh"
      [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"
    elif [ -s "/opt/homebrew/opt/nvm/nvm.sh" ]; then
      \. "/opt/homebrew/opt/nvm/nvm.sh"
    fi
    [ -s "/opt/homebrew/opt/nvm/etc/bash_completion.d/nvm" ] && \. "/opt/homebrew/opt/nvm/etc/bash_completion.d/nvm"
  }
  nvm() { _nvm_load; nvm "$@"; }
  node() { _nvm_load; node "$@"; }
  npm() { _nvm_load; npm "$@"; }
  npx() { _nvm_load; npx "$@"; }
fi

# xdg PATH first
path=("$HOME/.local/bin" $path)

# emacs mode (EDITOR=vim defaults to vi mode)
bindkey -e

# local overrides
[[ -f "$HOME/.zshrc.local" ]] && source "$HOME/.zshrc.local"
[[ -f ~/.secrets ]] && source ~/.secrets
