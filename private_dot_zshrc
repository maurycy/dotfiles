# Environment
export EDITOR=vim

export PATH="$HOME/.local/go/bin:$PATH"
export PATH="$HOME/google-cloud-sdk/bin:$PATH"
export PATH="$PATH:$HOME/.local/bin:/snap/bin"

# export GOPATH=$HOME/.local/go
# export PATH="$HOME/bin:$PATH"
# export CC="ccache gcc"
# export PSQL_HISTORY=/dev/null

# Shell options
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY

# History
HISTFILE=~/.zsh_history
HISTSIZE=
SAVEHIST=

# Prompt
autoload -Uz vcs_info
zstyle ':vcs_info:*' formats '(%b)'

precmd() {
  vcs_info
  PS1="%(?..[%?] )%D{%H:%M:%S.%N%p %Z} %n@%B%m%b %~ ${vcs_info_msg_0_} %# "
}

# Key bindings
bindkey -e
bindkey "^A" beginning-of-line
bindkey "^E" end-of-line

# Aliases
alias l='exa -l'
alias ll='ls -AcFl'
alias rg='rg -p'
alias be='bundle exec'

alias paw="pwgen -snyc 128"
alias pw='pwgen -snyc 128 1 | pbcopy'

alias yt='youtube-dlc -f bestvideo+bestaudio --write-all-thumbnails --write-sub --convert-subs srt'

alias f-rsync='/usr/local/bin/rsync -s -E -v -a -x --delete -l -u -H -P'
alias my-rsync='rsync -avhzrXANteEgoD --fileflags'
alias cc-rsync='rsync -v -A -X -H -go --numeric-ids -D -l -rtpx -N --fileflags --force-change --protect-args --ignore-errors'

# Clipboard (OSC 52 for remote, xclip/xsel for local)
osc52pbcopy() {
  local data esc
  data=$(cat | base64 | tr -d '\n')
  esc=$'\e]52;c;'$data$'\a'
  if [[ -n "$TMUX" ]]; then
    printf '\ePtmux;\e%s\e\\' "$esc" >/dev/tty
  else
    printf '%s' "$esc" >/dev/tty
  fi
}

osc52pbpaste() {
  printf '\e]52;c;?\a' >/dev/tty
  IFS=$'\a' read -rd '' _osc_reply || true
  printf '%s' "${_osc_reply#*52;c;}" | base64 --decode
}

_osc52_supported() {
  [[ "$TERM_PROGRAM" =~ ^(iTerm\.app|WezTerm)$ ]] || [[ "$TERM" == *kitty* ]] \
  || [[ "$TERM" == screen* ]] || [[ "$TERM" == tmux* ]]
}

if _osc52_supported; then
  alias pbcopy='osc52pbcopy'
  alias pbpaste='osc52pbpaste'
elif command -v xclip >/dev/null 2>&1; then
  alias pbcopy='xclip -selection clipboard'
  alias pbpaste='xclip -o -selection clipboard'
elif command -v xsel >/dev/null 2>&1; then
  alias pbcopy='xsel --clipboard --input'
  alias pbpaste='xsel --clipboard --output'
fi

# rbenv (lazy-loaded)
if [ -d "$HOME/.rbenv" ]; then
  export PATH="$HOME/.rbenv/bin:$HOME/.rbenv/shims:$PATH"
  _rbenv_load() {
    unset -f ruby gem bundle rbenv irb rake rails
    eval "$(~/.rbenv/bin/rbenv init - zsh)"
  }
  ruby() { _rbenv_load; ruby "$@"; }
  gem() { _rbenv_load; gem "$@"; }
  bundle() { _rbenv_load; bundle "$@"; }
  rbenv() { _rbenv_load; rbenv "$@"; }
  irb() { _rbenv_load; irb "$@"; }
  rake() { _rbenv_load; rake "$@"; }
  rails() { _rbenv_load; rails "$@"; }
fi

# nvm (lazy-loaded)
export NVM_DIR="$HOME/.nvm"
# Add default node to PATH for global npm binaries (codex, etc.)
if [ -d "$NVM_DIR/versions/node" ]; then
  _nvm_resolve_alias() {
    local alias="$1" target
    while [ -f "$NVM_DIR/alias/$alias" ]; do
      target=$(cat "$NVM_DIR/alias/$alias")
      [[ "$target" == v* ]] && { echo "$target"; return; }
      alias="$target"
    done
    # Check lts subdirectory
    [ -f "$NVM_DIR/alias/lts/$alias" ] && cat "$NVM_DIR/alias/lts/$alias" && return
    echo "$alias"
  }
  NODE_VER=$(_nvm_resolve_alias default)
  if [ -d "$NVM_DIR/versions/node/$NODE_VER" ]; then
    export PATH="$NVM_DIR/versions/node/$NODE_VER/bin:$PATH"
  else
    # Fallback: use latest installed version
    NODE_LATEST=$(ls -v "$NVM_DIR/versions/node" 2>/dev/null | tail -1)
    [ -n "$NODE_LATEST" ] && export PATH="$NVM_DIR/versions/node/$NODE_LATEST/bin:$PATH"
  fi
  unset -f _nvm_resolve_alias
fi
if [ -s "$NVM_DIR/nvm.sh" ]; then
  nvm() {
    unset -f nvm node npm npx
    \. "$NVM_DIR/nvm.sh"
    [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"
    nvm "$@"
  }
  node() { unset -f nvm node npm npx; \. "$NVM_DIR/nvm.sh"; node "$@"; }
  npm() { unset -f nvm node npm npx; \. "$NVM_DIR/nvm.sh"; npm "$@"; }
  npx() { unset -f nvm node npm npx; \. "$NVM_DIR/nvm.sh"; npx "$@"; }
fi

# pyenv
eval "$(pyenv virtualenv-init -)"

# Secrets (not in git)
[[ -f ~/.secrets ]] && source ~/.secrets
